print "Delvery logic by AlphaGamer"
print "version 0.2, build 32818"
print "3 units version."
print "MAX constant can be used is settings."
set MAX 4e20
print "Set to conected sorter's name to use as utype selector"
set useSorterSelector null
print "Unit type to use. Will be used if useSorterSelector null."
print "Naval units and dagger branch does not work."
print "Crawler, flare, mono, nova brances works fine."
print "Set to null if you want to use only selector."
set utype @flare
print "Minimum amount of links."
set minLinks 2
print "maximum time to bind unit in seconds. 0.2-2 recomended"
set maxBindTimeSec 1
print "Resource saver. Not recomended to set below 100."
set minRes 200
print "first prioItemsCount units can use priority system. Set 0 to off"
set prioItemsCount 0
print "Credit message. Must start with \n. Shorter = better."
print "Example: \nScheme by (your name) & (someone's name)"
set postMessage ""
print "item type, destination, item limit, max idle time before unbind, sec."
set item1 @copper
set dest1 null
set limit1 100
set unbindTimeout1 10
set item2 @copper
set dest2 null
set limit2 100
set unbindTimeout2 10
set item3 @copper
set dest3 null
set limit3 100
set unbindTimeout3 10
print "priority settings. set to null if you dont want to use item"
set primary @copper
set primaryMin 1000
set secondary @copper
set secondaryMin 1000
set tertiary @copper
set tertiaryMin 1000
set quaternary @copper
set quaternaryMin 1000
set quinary @copper
set quinaryMin 1000
set senary @copper
set senaryMin 1000
print "[red]^------END OF SETTINGS------^"
print "Also check custom code field at the end of code."
print "Do not touch stuff below. Please."
jump 0 lessThan @links minLinks
op mul temp @thisx 32818
op sub f1 temp @thisy
op mul f2 f1 2
op mul f3 f1 3
op floor prioItemsCount prioItemsCount 0
op max prioItemsCount prioItemsCount 0
op mul itemPryoMaxFlag f1 prioItemsCount
jump 62 notEqual primary null
set primaryMin -1
jump 64 notEqual secondary null
set secondaryMin -1
jump 66 notEqual tertiary null
set tertiaryMin -1
jump 68 notEqual quaternary null
set quaternaryMin -1
jump 70 notEqual quinary null
set quinaryMin -1
jump 72 notEqual senary null
set senaryMin -1
op add unbindTime1 @second 60
op add unbindTime2 @second 60
op add unbindTime3 @second 60
jump 134 equal useSorterSelector 0
sensor cfg useSorterSelector @config
jump 304 strictEqual cfg null
jump 134 strictEqual cfg cachedCfg
set cachedCfg cfg
sensor id cfg @id
op mul id id 2
op add @counter @counter id
set utype @mono
jump 127 always 0 0
set utype @poly
jump 127 always 0 0
set utype @mega
jump 127 always 0 0
set utype @quad
jump 127 always 0 0
set utype @flare
jump 127 always 0 0
set utype @zenith
jump 127 always 0 0
set utype @antumbra
jump 127 always 0 0
set utype @eclipse
jump 127 always 0 0
set utype @nova
jump 127 always 0 0
set utype @pulsar
jump 127 always 0 0
set utype @quasar
jump 127 always 0 0
set utype @vela
jump 127 always 0 0
set utype @atrax
jump 127 always 0 0
set utype @spiroct
jump 127 always 0 0
set utype @arkyid
jump 127 always 0 0
set utype @toxopid
jump 127 always 0 0
set utype @merui
jump 127 always 0 0
set utype @collaris
jump 127 always 0 0
set utype @disrupt
jump 127 always 0 0
set utype @collaris
jump 127 always 0 0
set utype @quell
jump 127 always 0 0
set utype @anthicus
jump 127 always 0 0
jump 129 equal isIdle 0
print "[blue][Info]: []No item requests.\n"
print "Selected: [cyan]"
print utype
print "[]\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=\n\nLogic by [#83]AlphaGamer[]"
print postMessage
printflush message1
jump 138 equal switch1 null
sensor enabled switch1 @enabled
jump 138 strictEqual enabled 1
jump 308 always 0 0
jump 166 equal prioItemsCount 0
sensor core.dead core @dead
jump 165 equal core.dead 1
sensor inCore core primary
jump 145 lessThan inCore primaryMin
set prioItem primary
jump 166 always 0 0
sensor inCore core secondary
jump 149 lessThan inCore secondaryMin
set prioItem secondary
jump 166 always 0 0
sensor inCore core tertiary
jump 153 lessThan inCore tertiaryMin
set prioItem tertiary
jump 166 always 0 0
sensor inCore core quaternary
jump 157 lessThan inCore quaternaryMin
set prioItem quaternary
jump 166 always 0 0
sensor inCore core quinary
jump 161 lessThan inCore quinaryMin
set prioItem quinary
jump 166 always 0 0
sensor inCore core senary
jump 165 lessThan inCore senaryMin
set prioItem senary
jump 166 always 0 0
set prioItem null
set isIdle 1
set uflag f1
set item item1
set dest dest1
set limit limit1
set unbindTimeout unbindTimeout1
set workUnit u1
set unbindTime unbindTime1
op add return @counter 2
jump 204 always 0 0
set item1 item
set unbindTime1 unbindTime
set u1 workUnit
set uflag f2
set item item2
set dest dest2
set limit limit2
set unbindTimeout unbindTimeout2
set workUnit u2
set unbindTime unbindTime2
op add return @counter 2
jump 204 always 0 0
set item2 item
set unbindTime2 unbindTime
set u2 workUnit
set uflag f3
set item item3
set dest dest3
set limit limit3
set unbindTimeout unbindTimeout3
set workUnit u3
set unbindTime unbindTime3
op add return @counter 2
jump 204 always 0 0
set item3 item
set unbindTime3 unbindTime
set u3 workUnit
jump 357 always 0 0
sensor dest.dead dest @dead
jump 212 strictEqual dest.dead 0
ubind workUnit
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
set workUnit null
set @counter return
sensor workUnit.dead workUnit @dead
jump 282 strictEqual workUnit.dead 1
sensor workUnit.controller workUnit @controller
jump 282 notEqual workUnit.controller @this
sensor workUnit.flag workUnit @flag
jump 232 strictEqual workUnit.flag uflag
jump 282 always 0 0
op add cantBindTimeout @second maxBindTimeSec
jump 282 greaterThan @second cantBindTimeout
ubind utype
sensor @unit.flag @unit @flag
jump 220 notEqual @unit.flag 0
sensor @unit.dead @unit @dead
jump 227 strictEqual @unit.dead 0
set @counter return
sensor itemCap @unit @itemCapacity
ucontrol flag uflag 0 0 0 0
ucontrol stop 0 0 0 0 0
ucontrol boost 1 0 0 0 0
set workUnit @unit
ubind workUnit
set isIdle 0
sensor inUnit @unit @firstItem
jump 252 equal inUnit null
jump 273 notEqual inUnit item
sensor dest.x dest @x
sensor dest.y dest @y
ucontrol pathfind dest.x dest.y 0 0 0
sensor inDest dest item
jump 244 lessThan inDest limit
jump 293 lessThanEq unbindTime @second
set @counter return
op add unbindTime @second unbindTimeout
ucontrol itemDrop dest 999 0 0 0
sensor itemsCount @unit @totalItems
jump 249 lessThanEq itemsCount 0
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
jump 262 lessThanEq uflag itemPryoMaxFlag
jump 258 notEqual item null
jump 293 lessThanEq unbindTime @second
set @counter return
sensor inCore core item
jump 264 greaterThanEq inCore minRes
jump 293 lessThanEq unbindTime @second
set @counter return
jump 256 equal prioItem null
set item prioItem
op add unbindTime @second unbindTimeout
ucontrol itemTake core item 99 0 0
sensor itemsCount @unit @totalItems
jump 269 greaterThanEq itemsCount itemCap
op sub @counter return 1
sensor dest.x dest @x
sensor dest.y dest @y
ucontrol pathfind dest.x dest.y 0 0 0
op sub @counter return 1
ulocate building core false _ coreX coreY _ core
sensor inCore core inUnit
sensor coreCap core @itemCapacity
jump 280 greaterThanEq inCore coreCap
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
set @counter return
ucontrol itemDrop @air 999 0 0 0
jump 252 always 0 0
jump 285 greaterThan uflag itemPryoMaxFlag
jump 285 notEqual prioItem null
set @counter return
sensor inDest dest item
jump 288 lessThan inDest limit
set @counter return
sensor core.dead core @dead
jump 219 equal core.dead 1
sensor inCore core item
jump 219 greaterThan inCore minRes
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
op add @counter @counter isNearCore
set @counter return
ucontrol itemDrop @air 999 0 0 0
ucontrol flag 0 0 0 0 0
ucontrol unbind 0 0 0 0 0
set workUnit null
set @counter return
printflush null
print "Select unit type.\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=\n\nLogic by [#83]AlphaGamer[]"
print postMessage
printflush message1
ubind u1
jump 324 equal @unit null
sensor flag @unit @flag
jump 315 equal flag f1
sensor ctrl @unit @controller
jump 323 notEqual ctrl @this
jump 323 notEqual flag 0
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
jump 324 equal isNearCore 0
ucontrol itemDrop @air 999 0 0 0
ucontrol unbind 0 0 0 0 0
set u1 null
ubind u2
jump 340 equal @unit null
sensor flag @unit @flag
jump 331 equal flag f2
sensor ctrl @unit @controller
jump 339 notEqual ctrl @this
jump 339 notEqual flag 0
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
jump 340 equal isNearCore 0
ucontrol itemDrop @air 999 0 0 0
ucontrol unbind 0 0 0 0 0
set u2 null
ubind u3
jump 356 equal @unit null
sensor flag @unit @flag
jump 347 equal flag f3
sensor ctrl @unit @controller
jump 355 notEqual ctrl @this
jump 355 notEqual flag 0
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
jump 356 equal isNearCore 0
ucontrol itemDrop @air 999 0 0 0
ucontrol unbind 0 0 0 0 0
set u3 null
jump 75 always 0 0
print "custom code field."
print "Can be used to aim turrets, toggle projectors, or something else"
print "[red]prohibed: stop, end, wait"
print "[yellow]not recomended: jump backward, unit operations"
print "[lime]your code starts here."
set unitIndex 0
op sub dt @second timer
set timer @second
write dt cell1 0
sensor timescale @this @timescale
op notEqual podEnabled timescale 2.5
control enabled projector1 podEnabled 0 0 0
print "[lime]your code ends here."
jump 75 always 0 0
print "github.com/ols45234/mlogProjects"
