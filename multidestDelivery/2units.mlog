print "Delvery logic by AlphaGamer"
print "version 0.2, build 32866"
print "2 units version."
print "MAX constant can be used is settings."
set MAX 4e20
print "Unit type to use. Will be used if useSorterSelector null."
print "Naval units and dagger branch does not work."
print "Crawler, flare, mono, nova brances works fine."
print "Set to null if you want to use selector or utype priority."
set utype @flare
print "Check utype priority."
set useUTypePrio 0
print "Minimum amount of links."
set minLinks 2
print "maximum time to bind unit in seconds. 0.2-2 recomended"
set maxBindTimeSec 1
print "Resource saver. Not recomended to set below 100."
set minRes 200
print "first prioItemsCount units can use priority system. Set 0 to off. Please do not set to 2"
set prioItemsCount 0
print "Credit message. Must start with \n. Shorter = better."
print "Example: \nScheme by (your name) & (someone's name)"
set postMessage ""
print "Set to conected sorter's name to use as utype selector"
set useSorterSelector sorter1
print "item type, destination, item limit, max idle time before unbind, sec."
set item1 @copper
set dest1 null
set limit1 10
set unbindTimeout1 10
set item2 @copper
set dest2 null
set limit2 10
set unbindTimeout2 10
print "Please, do not touch this jump"
jump 85 notEqual f1 null
print "priority settings. Set to null if you dont want to use item."
set primary @copper
set primaryMin 1000
set secondary @copper
set secondaryMin 1000
set tertiary @copper
set tertiaryMin 1000
set quaternary @copper
set quaternaryMin 1000
set quinary @copper
set quinaryMin 1000
set senary @copper
set senaryMin 1000
print "Unit type priority. Values can be null."
set utype1 @flare
set utype2 @mono
set utype3 @nova
set utype4 @poly
set utype5 @pulsar
set utype6 @atrax
set utype7 null
print "[red]^------END OF SETTINGS------^"
print "Also check custom code field at the end of code."
print "Do not touch stuff below. Please."
jump 0 lessThan @links minLinks
set selectorErekir ""
op mul f1 @thisx @maph
op sub f1 f1 @thisy
op mul f1 f1 42
op mul f2 f1 2
op floor prioItemsCount prioItemsCount 0
op max prioItemsCount prioItemsCount 0
op mul itemPryoMaxFlag f1 prioItemsCount
jump 71 notEqual primary null
set primaryMin -1
jump 73 notEqual secondary null
set secondaryMin -1
jump 75 notEqual tertiary null
set tertiaryMin -1
jump 77 notEqual quaternary null
set quaternaryMin -1
jump 79 notEqual quinary null
set quinaryMin -1
jump 81 notEqual senary null
set senaryMin -1
op add unbindTime1 @second 60
op add unbindTime2 @second 60
jump 104 equal cachedLinks @links
jump 24 always 0 0
set cachedLinks @links
jump 89 notEqual useSorterSelector null
jump 89 equal useUTypePrio 0
set utype null
ubind @merui
jump 103 notEqual @unit null
ubind @cleroi
jump 103 notEqual @unit null
ubind @disrupt
jump 103 notEqual @unit null
ubind @collaris
jump 103 notEqual @unit null
ubind @quell
jump 103 notEqual @unit null
ubind @anthicus
jump 103 notEqual @unit null
set selectorErekir ""
jump 104 always 0 0
set selectorErekir "=|=|=|=\n=|=\n"
jump 164 equal useSorterSelector 0
sensor cfg useSorterSelector @config
jump 393 strictEqual cfg null
jump 164 strictEqual cfg cachedCfg
set cachedCfg cfg
sensor id cfg @id
op mul id id 2
op add @counter @counter id
set utype @mono
jump 156 always 0 0
set utype @poly
jump 156 always 0 0
set utype @mega
jump 156 always 0 0
set utype @quad
jump 156 always 0 0
set utype @flare
jump 156 always 0 0
set utype @zenith
jump 156 always 0 0
set utype @antumbra
jump 156 always 0 0
set utype @eclipse
jump 156 always 0 0
set utype @nova
jump 156 always 0 0
set utype @pulsar
jump 156 always 0 0
set utype @quasar
jump 156 always 0 0
set utype @vela
jump 156 always 0 0
set utype @atrax
jump 156 always 0 0
set utype @spiroct
jump 156 always 0 0
set utype @arkyid
jump 156 always 0 0
set utype @toxopid
jump 156 always 0 0
set utype @merui
jump 156 always 0 0
set utype @collaris
jump 156 always 0 0
set utype @disrupt
jump 156 always 0 0
set utype @cleroi
jump 156 always 0 0
set utype @quell
jump 156 always 0 0
set utype @anthicus
jump 156 always 0 0
printflush null
print "Selected: [#AEC2F1]"
print utype
print "[]\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n"
print selectorErekir
print "\nLogic by [#83]AlphaGamer[]"
print postMessage
printflush message1
jump 168 equal switch1 null
sensor enabled switch1 @enabled
jump 168 strictEqual enabled 1
jump 399 always 0 0
jump 196 equal prioItemsCount 0
sensor core.dead core @dead
jump 195 equal core.dead 1
sensor inCore core primary
jump 175 lessThan inCore primaryMin
set prioItem primary
jump 196 always 0 0
sensor inCore core secondary
jump 179 lessThan inCore secondaryMin
set prioItem secondary
jump 196 always 0 0
sensor inCore core tertiary
jump 183 lessThan inCore tertiaryMin
set prioItem tertiary
jump 196 always 0 0
sensor inCore core quaternary
jump 187 lessThan inCore quaternaryMin
set prioItem quaternary
jump 196 always 0 0
sensor inCore core quinary
jump 191 lessThan inCore quinaryMin
set prioItem quinary
jump 196 always 0 0
sensor inCore core senary
jump 195 lessThan inCore senaryMin
set prioItem senary
jump 196 always 0 0
set prioItem null
set uflag f1
set item item1
set dest dest1
set limit limit1
set unbindTimeout unbindTimeout1
set workUnit u1
set unbindTime unbindTime1
op add return @counter 2
jump 221 always 0 0
set item1 item
set unbindTime1 unbindTime
set u1 workUnit
set uflag f2
set item item2
set dest dest2
set limit limit2
set unbindTimeout unbindTimeout2
set workUnit u2
set unbindTime unbindTime2
op add return @counter 2
jump 221 always 0 0
set item2 item
set unbindTime2 unbindTime
set u2 workUnit
jump 436 always 0 0
sensor dest.dead dest @dead
jump 229 strictEqual dest.dead 0
ubind workUnit
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
set workUnit null
set @counter return
sensor workUnit.dead workUnit @dead
jump 371 strictEqual workUnit.dead 1
sensor workUnit.controller workUnit @controller
jump 371 notEqual workUnit.controller @this
sensor workUnit.flag workUnit @flag
jump 321 strictEqual workUnit.flag uflag
jump 371 always 0 0
op add cantBindTimeout @second maxBindTimeSec
jump 309 notEqual utype null
ubind utype1
sensor dead @unit @dead
jump 248 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 248 greaterThanEq @second cantBindTimeout
ubind utype1
sensor @unit.flag @unit @flag
jump 243 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype2
sensor dead @unit @dead
jump 258 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 258 greaterThanEq @second cantBindTimeout
ubind utype2
sensor @unit.flag @unit @flag
jump 253 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype3
sensor dead @unit @dead
jump 268 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 268 greaterThanEq @second cantBindTimeout
ubind utype3
sensor @unit.flag @unit @flag
jump 263 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype4
sensor dead @unit @dead
jump 278 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 278 greaterThanEq @second cantBindTimeout
ubind utype4
sensor @unit.flag @unit @flag
jump 273 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype5
sensor dead @unit @dead
jump 288 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 288 greaterThanEq @second cantBindTimeout
ubind utype5
sensor @unit.flag @unit @flag
jump 283 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype6
sensor dead @unit @dead
jump 298 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 298 greaterThanEq @second cantBindTimeout
ubind utype6
sensor @unit.flag @unit @flag
jump 293 notEqual @unit.flag 0
jump 316 always 0 0
ubind utype7
sensor dead @unit @dead
jump 308 equal dead 1
set anchor @unit
op add cantBindTimeout @second maxBindTimeSec
jump 308 greaterThanEq @second cantBindTimeout
ubind utype7
sensor @unit.flag @unit @flag
jump 303 notEqual @unit.flag 0
jump 316 always 0 0
set @counter return
jump 315 greaterThan @second cantBindTimeout
ubind utype
sensor @unit.flag @unit @flag
jump 309 notEqual @unit.flag 0
sensor @unit.dead @unit @dead
jump 316 strictEqual @unit.dead 0
set @counter return
sensor itemCap @unit @itemCapacity
ucontrol flag uflag 0 0 0 0
ucontrol stop 0 0 0 0 0
ucontrol boost 1 0 0 0 0
set workUnit @unit
ubind workUnit
set isIdle 0
sensor inUnit @unit @firstItem
jump 341 equal inUnit null
jump 362 notEqual inUnit item
sensor dest.x dest @x
sensor dest.y dest @y
ucontrol pathfind dest.x dest.y 0 0 0
sensor inDest dest item
jump 333 lessThan inDest limit
jump 382 lessThanEq unbindTime @second
set @counter return
op add unbindTime @second unbindTimeout
ucontrol itemDrop dest 999 0 0 0
sensor itemsCount @unit @totalItems
jump 338 lessThanEq itemsCount 0
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
jump 351 lessThanEq uflag itemPryoMaxFlag
jump 347 notEqual item null
jump 382 lessThanEq unbindTime @second
set @counter return
sensor inCore core item
jump 353 greaterThanEq inCore minRes
jump 382 lessThanEq unbindTime @second
set @counter return
jump 345 equal prioItem null
set item prioItem
op add unbindTime @second unbindTimeout
ucontrol itemTake core item 999 0 0
sensor itemsCount @unit @totalItems
jump 358 greaterThanEq itemsCount itemCap
op sub @counter return 1
sensor dest.x dest @x
sensor dest.y dest @y
ucontrol pathfind dest.x dest.y 0 0 0
op sub @counter return 1
ulocate building core false _ coreX coreY _ core
sensor inCore core inUnit
sensor coreCap core @itemCapacity
jump 369 greaterThanEq inCore coreCap
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
set @counter return
ucontrol itemDrop @air 999 0 0 0
jump 341 always 0 0
jump 374 greaterThan uflag itemPryoMaxFlag
jump 374 notEqual prioItem null
set @counter return
sensor inDest dest item
jump 377 lessThan inDest limit
set @counter return
sensor core.dead core @dead
jump 236 equal core.dead 1
sensor inCore core item
jump 236 greaterThan inCore minRes
set @counter return
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol within coreX coreY 2 isNearCore 0
op add @counter @counter isNearCore
set @counter return
ucontrol itemDrop core 999 0 0 0
ucontrol itemDrop @air 999 0 0 0
ucontrol flag 0 0 0 0 0
ucontrol unbind 0 0 0 0 0
set workUnit null
set @counter return
printflush null
print "Select unit type.\n=|=|=|=\n=|=|=|=\n=|=|=|=\n=|=|=|=\n"
print selectorErekir
print "\nLogic by [#83]AlphaGamer[]"
print postMessage
printflush message1
ubind u1
jump 415 equal @unit null
sensor flag @unit @flag
jump 406 equal flag f1
sensor ctrl @unit @controller
jump 414 notEqual ctrl @this
jump 414 notEqual flag 0
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
jump 415 equal isNearCore 0
ucontrol itemDrop @air 999 0 0 0
ucontrol unbind 0 0 0 0 0
set u1 null
ubind u2
jump 431 equal @unit null
sensor flag @unit @flag
jump 422 equal flag f2
sensor ctrl @unit @controller
jump 430 notEqual ctrl @this
jump 430 notEqual flag 0
ucontrol flag 0 0 0 0 0
ulocate building core false _ coreX coreY _ core
ucontrol pathfind coreX coreY 0 0 0
ucontrol itemDrop core 999 0 0 0
ucontrol within coreX coreY 4 isNearCore 0
jump 431 equal isNearCore 0
ucontrol itemDrop @air 999 0 0 0
ucontrol unbind 0 0 0 0 0
set u2 null
jump 436 always 0 0
print "custom code field."
print "Can be used to aim turrets, toggle projectors, or something else"
print "[red]prohibed: stop, end, wait"
print "[yellow]not recomended: jump backward, unit operations"
print "[lime]your code starts here."
print "[lime]your code ends here."
jump 83 always 0 0
print "github.com/ols45234/mlogProjects"
